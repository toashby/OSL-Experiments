#define nSPHERES 1

struct sphere{
    float m_radius; 
    vector m_position;
    color m_color;
};

shader Example(
output color COLOR = 0,
float MAX_DIST = 1000,
float MAX_STEPS = 100,
float SURFACE_DIST = 0.001,
vector LIGHT_POS = vector(1.5, 5.8, 6.0)
)
{   
    color hit_color = color(1,0,0);

    sphere spheres[nSPHERES];
  
    spheres[0] = sphere(0.1,
                        vector(0.2,0.2,0.2),
                        vector(0,1,0));  

    point CamPos;//get the camera position
    float L;
    if (raytype("camera")){
        getattribute("path:ray_length", L);
        CamPos=L*I+P;
    }
    
    
    float capsuleSDF(vector p, float h, float r){
        vector tp = p;
        tp[1] -= clamp(p[1], 0.0, h); 
        float line_dist = length(tp) - r;
        return line_dist;
    }
    
    float boxSDF(vector p, vector s){
        return length(max(abs(p)-s, 0.0));
    }


    float getDist(vector march_edge){
        float closest_dist = 2147483647; //max float
        //test
        for(int i = 0; i < arraylength(spheres); i++){
            float d = distance(mod(march_edge, 0.4), spheres[i].m_position)-spheres[i].m_radius;
            if(closest_dist > d){
                closest_dist = d;
                hit_color = spheres[i].m_color;
            }
        }
        
        float plane_dist = march_edge[1]+0.25; // y pos, this is the sdf for plane
        
        //closest_dist = min(closest_dist, plane_dist);
        if(plane_dist < closest_dist){
            closest_dist = plane_dist;
            hit_color = color(0.1, 0.4, 0.1);
        }
        
        
        //add more shapes here!
        
        //vertical capsule!
        float cap_dist = capsuleSDF(march_edge - vector(0, 0.2, -0.2), 0.15, 0.05);
        if(cap_dist < closest_dist){
            closest_dist = cap_dist;
            hit_color = color(0.8, 0.2, 0.1);
        } 
        
        //box!
        float box_dist = boxSDF(march_edge - vector(-0.3,-0.15,0.2), vector(0.1, 0.1, 0.1));
        if(box_dist < closest_dist){
            closest_dist = box_dist;
            hit_color = color(0.1, 0.3, 0.3);
        } 
        
        
        return closest_dist;
    }


    vector getNormal(vector p){
        //hack
        float d = getDist(p);
        vector e = vector(.01, 0, 0); //Does osl support swizzle?
        vector f = vector(0, .01, 0);
        vector g = vector(0, 0, .01);

        vector n = d - vector(getDist(p-e), getDist(p-f), getDist(p-g));
        return normalize(n);
    }


    float rayMarch(vector ray_origin, vector ray_direction){

        float dist_to_origin = 0;
        
        for(int j = 0; j < MAX_STEPS; j++){
            vector march_edge = ray_origin + dist_to_origin * ray_direction;
            float ds = getDist(march_edge);
            dist_to_origin += ds;
        
            if(ds < SURFACE_DIST || dist_to_origin > MAX_DIST) break;
        }
        
        return dist_to_origin;
    }


    color getLight(vector p){
        vector li = normalize(LIGHT_POS-p); //get vector from hit pos to light
        vector norm = getNormal(p); //get a hacky normal at hit point
        
        color diff = clamp(dot(norm, li), 0.0, 1.0) * (hit_color);
        
        float dist_to_light = rayMarch(p + norm * SURFACE_DIST*2.0, li); // add a small amount to p so the march loop doesn't break immediately
        if (dist_to_light < length(LIGHT_POS - p)) diff *= 0.25;
        
        return diff;
        //return 1.0; //temp value
    }
    //spheres[hit_sphere].m_color

    float d = rayMarch(CamPos, I);
    
    //calculate normals here!
    vector pp = CamPos + I * d;
    color diff = getLight(pp);
    
    COLOR = vector(diff);
    //COLOR = getNormal(pp); //to test visualise the normals 
}
